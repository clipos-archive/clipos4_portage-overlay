# Copyright 1999-2012 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header: /var/cvsroot/gentoo-x86/sys-apps/sysvinit/sysvinit-2.88-r7.ebuild,v 1.7 2014/05/17 15:05:12 swift Exp $

EAPI=4

inherit eutils toolchain-funcs flag-o-matic verictl2

DESCRIPTION="/sbin/init - parent of all processes"
HOMEPAGE="http://savannah.nongnu.org/projects/sysvinit"
SRC_URI="mirror://nongnu/${PN}/${P}dsf.tar.bz2"

LICENSE="GPL-2"
SLOT="0"
KEYWORDS="~alpha amd64 arm hppa ~ia64 ~m68k ~mips ppc ppc64 ~s390 ~sh ~sparc x86"
IUSE="selinux ibm static kernel_FreeBSD"
IUSE="${IUSE} hardened clip clip-devel clip-devstation clip-livecd clip-bare"

RDEPEND="
	selinux? (
		>=sys-libs/libselinux-1.28
		sec-policy/selinux-shutdown
	)
	!clip? ( !<sys-apps/util-linux-2.20 )"
DEPEND="${RDEPEND}
	virtual/os-headers"

S=${WORKDIR}/${P}dsf

src_prepare() {
	epatch "${FILESDIR}"/${PN}-2.86-kexec.patch #80220
	epatch "${FILESDIR}"/${PN}-2.86-shutdown-single.patch #158615
	epatch "${FILESDIR}"/${P}-makefile.patch #319197
	epatch "${FILESDIR}"/${P}-selinux.patch #326697
	epatch "${FILESDIR}"/${P}-shutdown-h.patch #449354
	sed -i '/^CPPFLAGS =$/d' src/Makefile || die

	# last/lastb/mesg/mountpoint/sulogin/utmpdump/wall have moved to util-linux
	sed -i -r \
		-e '/^(USR)?S?BIN/s:\<(last|lastb|mesg|mountpoint|sulogin|utmpdump|wall)\>::g' \
		-e '/^MAN[18]/s:\<(last|lastb|mesg|mountpoint|sulogin|utmpdump|wall)[.][18]\>::g' \
		src/Makefile || die

	# pidof has moved to >=procps-3.3.9
	sed -i -r \
		-e '/\/bin\/pidof/d' \
		-e '/^MAN8/s:\<pidof.8\>::g' \
		src/Makefile || die

	if use clip; then
		epatch "${FILESDIR}/${PN}-2.86-clip-cap_inh.patch"
		epatch "${FILESDIR}/${PN}-2.88-clip-wide-output.patch"
	fi
	epatch "${FILESDIR}"/${PN}-2.88-r12-fix-missing-header.patch

	# Mung inittab for specific architectures
	cd "${WORKDIR}"
	if use clip; then
		cp "${FILESDIR}"/inittab.clip-r7 inittab || die "cp inittab.clip"
		if use !clip-devel; then
			sed -i -e '/.*agetty.*/d' inittab || die "sed inittab.clip failed"
		fi
	elif use clip-livecd; then
		cp "${FILESDIR}"/inittab.livecd-r2 inittab || die "cp inittab.livecd"
	else
		cp "${FILESDIR}"/inittab-2.87 inittab || die "cp inittab"
	fi
	if use clip-bare; then
		echo "c1:2345:respawn:/sbin/agetty 38400 tty1 linux" >> inittab
		if [[ "${ARCH}" = arm ]] ; then 
			echo "s0:2345:respawn:/sbin/agetty 115200 ttyS0 linux" >> inittab
		fi
	elif use clip-livecd; then
		echo "s0:12345:respawn:/sbin/agetty -8 -a root 115200 ttyS0 linux" >> inittab
	fi

	local insert=()
	use ppc && insert=( '#psc0:12345:respawn:/sbin/agetty 115200 ttyPSC0 linux' )
	use arm && insert=( '#f0:12345:respawn:/sbin/agetty 9600 ttyFB0 vt100' )
	use arm64 && insert=( 'f0:12345:respawn:/sbin/agetty 9600 ttyAMA0 vt100' )
	use hppa && insert=( 'b0:12345:respawn:/sbin/agetty 9600 ttyB0 vt100' )
	use s390 && insert=( 's0:12345:respawn:/sbin/agetty 38400 console dumb' )
	if use ibm ; then
		insert+=(
			'#hvc0:2345:respawn:/sbin/agetty -L 9600 hvc0'
			'#hvsi:2345:respawn:/sbin/agetty -L 19200 hvsi0'
		)
	fi
	(use arm || use mips || use sh || use sparc) && sed -i '/ttyS0/s:#::' inittab
	if use kernel_FreeBSD ; then
		sed -i \
			-e 's/linux/cons25/g' \
			-e 's/ttyS0/cuaa0/g' \
			-e 's/ttyS1/cuaa1/g' \
			inittab #121786
	fi

	if use x86 || use amd64 ; then
		sed -i \
			-e '/ttyS[01]/s:9600:115200:' \
			inittab
	fi
	if ! use clip; then
		if [[ ${#insert[@]} -gt 0 ]] ; then
			printf '%s\n' '' '# Architecture specific features' "${insert[@]}" >> inittab
		fi
	fi
}

src_compile() {
	local myconf

	tc-export CC
	append-lfs-flags
	export DISTRO= #381311
	use static && append-ldflags -static
	use selinux && myconf=WITH_SELINUX=yes
	emake -C src ${myconf} || die
}

src_install() {
	emake -C src install ROOT="${D}"
	dodoc README doc/*

	insinto /etc
	doins "${WORKDIR}"/inittab

	# Provided by baselayout-clip
	if ! use clip && ! use clip-devstation && ! use clip-livecd; then
		doinitd "${FILESDIR}"/{reboot,shutdown}.sh
	fi

	# dead symlink
	rm -f "${D}"/usr/bin/lastb

	if use clip-bare; then
		exeinto /bin
		doexe "${FILESDIR}"/login.tty
	fi
}

pkg_postinst() {
	# Reload init to fix unmounting problems of / on next reboot.
	# This is really needed, as without the new version of init cause init
	# not to quit properly on reboot, and causes a fsck of / on next reboot.
	if [[ ${ROOT} == / ]] ; then
		# Do not return an error if this fails
		/sbin/telinit U &>/dev/null
	fi
	elog "The last/lastb/mesg/mountpoint/sulogin/utmpdump/wall tools have been moved to"
	elog "sys-apps/util-linux. The pidof tool has been moved to sys-process/procps."
}

pkg_predeb() {
	# CAP_SYS_BOOT + CAP_NET_ADMIN + CLSM_PRIV_NETOTHER
	doverictld2 /sbin/halt er 'SYS_BOOT|NET_ADMIN' - 'SYS_BOOT|NET_ADMIN' n
}
